# The Dutch National Flag problem was proposed by Edsger Dijkstra. The flag
# of the Netherlands consists of three colors: Red, White, and Blue. We can
# tag integers as having one of these colors:

data Red: int -o coloredint
   | White: int -o coloredint
   | Blue: int -o coloredint ;;


# The input and output of the Dutch National Flag problem is a list of colored
# integers. The input is an arbitrary list, and the red integers (in the order
# they appeared in the original list) followed by the white integers (in the 
# order they appeared in the original list) followed by the blue integers (in 
# the order they appeard in the original list). 

data Nil: list
   | Cons: coloredint -o list -o list ;;


# The catch is that we are only allowed to traverse any part of the list once.
# We can iterate over the original list, of course, but we can't iterate over 
# anything else, so the usual "append" function can be thrown out right off the
# bat, since it iterates over one of the lists given as input.

# The difference list solution uses an intermediate data structure of three
# (list -o list)s that hold the partitioned list beginnings while we recurse
# over the list end. At each step, we pull something off the list end and tack
# it on to the appropriate list beginning, and once we reach the end of the
# unpartioned list, we concatenate all three partitions.

val dutch' = thunk rec loop : 
    (list -o list) 
      -> (list -o list) 
        -> (list -o list) 
          -> list 
            -> F list is 
  fun reds : (list -o list) -> 
  fun whites : (list -o list) -> 
  fun blues : (list -o list) -> 
  fun xs : list -> 
    match xs with
      | Nil -> return (reds (whites (blues Nil)))
      | Cons x xs -> 
         (match x with 
            | Red i -> 
                force loop 
                  ([hole: list] reds (Cons x hole)) 
                  whites 
                  blues 
                  xs
            | White i ->  
                force loop 
                  reds  
                  ([hole: list] whites (Cons x hole)) 
                  blues 
                  xs
            | Blue i -> 
                force loop 
                  reds 
                  whites 
                  ([hole: list] blues (Cons x hole)) 
                  xs) ;;

val dutch = thunk fun xs : list -> 
  force dutch' ([x: list] x) ([x: list] x) ([x: list] x) xs ;;

force dutch 
  (Cons (White 4) 
   (Cons (Blue 6)
    (Cons (Red 1)  
     (Cons (White 5) 
      (Cons (Blue 7) (Cons (Red 2) (Cons (Red 3) (Cons (Blue 8) Nil)))))))) ;;

